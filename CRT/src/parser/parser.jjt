
/** 
 * Parser for the constraints.
 * by Vincent Hugot, 2009-10-4
 
 * This grammar has the following advantages over the one which was suggested:
 
  * More robust: Won't die if there are too many line feeds at the beginning or end...
  * Support for negative integers (the other did not!!)
  * AST expression trees correspond to the intuitive idea:
    +(2,3) instead of Exp(2,Exp(+,3)); visitors will be much
    easier to implement...
  * Expressions are (correctly) left-associative
  * Expressions support operator priorities
  * Supports parentheses to override priorities
    (the other had no priorities and no parens to explicitely
     specify them... the only possible way to get correct nesting would
     have been to introduce intermediary variables... a mess !)
  * Supports several constraints on one line, comma-separated
     
 * ... and the following difference
  * Domains must be grouped at beginning of file
    (can be seen as a restriction; 
     I see it as a cleaner way of doing things but YMMW)
  
**/

options {
	MULTI=true;
	VISITOR=true;
	STATIC=false;
	ERROR_REPORTING=true; /* https://javacc.dev.java.net/doc/apiroutines.html Customizing Error Messages */
  //NODE_PACKAGE="parser";
    // For some reason, this option generates brain-dead "import .*"
    // which prevent compilation of generated files. So we'll do without it.
}

PARSER_BEGIN(Parser)
    public class Parser {}
PARSER_END(Parser)


/** LEXER */

SKIP : { 
  " " | "\r" | "\t" | "\n" 
  /* Switch to comment block mode */
  | < OPEN_COMMENT_BLOCK : "/*"> : IN_COMMENT_BLOCK
}  

TOKEN : {

  /** Identifiers */

    < IDENTIFIER :  
      <LETTER> 
      | 
      (( <LETTER> | "_" ) ( <LETTER> | <DIGIT> | <SPECIALSYMB> )*) 
    >
  | < INTEGER :     ("-")? (<DIGIT>)+ >
  | < DIGIT :       ["0"-"9"] >
  | < LETTER :      ["a"-"z", "A"-"Z"] >
  | < SPECIALSYMB : ["_", "'"] >

  /** Symbols */

  | < LB:     "[" >
  | < RB:     "]" >
  | < LP:     "(" >
  | < RP:     ")" >
  | < BOUNDS: ",...,"|"..." >
  | < COMMA:  "," >
  | < SEMICOLON: ";" >
  
  /** OPERATORS */

  | < DOMAIN_DEF:   ":" >

  | < PLUS:         "+" >
  | < MINUS:        "-">
  | < TIMES:        "*" >
  | < DIV:          "/" >

  | < NEQ:          "/="|"<>" >
  | < EQ:           "=" >

  | < LEQ:          "=<" > 
  | < GEQ:          ">=" >
  
  | < G:            ">" >
  | < L:            "<" >
  
} // END TOKENS 


<IN_COMMENT_BLOCK> SKIP : {
  "*/" : DEFAULT
}


<IN_COMMENT_BLOCK> MORE : {
  <~[]> 
}


MORE : {
    "//": IN_SINGLE_LINE_COMMENT 
}

      
< IN_SINGLE_LINE_COMMENT > SPECIAL_TOKEN : {
    < SINGLE_LINE_COMMENT: "\n"|"\r"|"\r\n" > : DEFAULT 
}

      
< IN_SINGLE_LINE_COMMENT > MORE : {
    <  ~[] >
} 


/** PARSER */

/* Rroot node */
SimpleNode constraints() : {} 
{
  domain_definition() (<COMMA> domain_definition())*
  <SEMICOLON>
  constraint() (<COMMA> constraint()) *
  <EOF> { return jjtThis; }
}

void domain_definition() #domain : {
	jjtThis.setLine(getToken(1).beginLine);
}
{
  variable() <DOMAIN_DEF> <LB> integer() <BOUNDS> integer() <RB>
}


void variable() #var : {
	jjtThis.setLine(getToken(1).beginLine);
}
{
  <IDENTIFIER>
    { 
       jjtThis.name = token.image; 
    }
}

void integer() #integer : {
	jjtThis.setLine(getToken(1).beginLine);
	String value;
}
{
  <INTEGER>
    {
      /*jjtThis.value = Integer.parseInt(token.image);*/
	  value = token.image; // String, then parsed with NumberFormatException
      jjtThis.setValue(value);
    }
}

void constraint() : { Token t;}
{
  expression()
  (
    (t= <EQ> expression() {jjtThis.setLine(t.beginLine);} #eq(2))
    |
    (t=<NEQ> expression() {jjtThis.setLine(t.beginLine);} #neq(2))
    |
    (t=<LEQ> expression() {jjtThis.setLine(t.beginLine);} #leq(2))
    |
    (t=<GEQ> expression() {jjtThis.setLine(t.beginLine);} #geq(2))
    |
    (t=  <G> expression() {jjtThis.setLine(t.beginLine);} #g(2))
    |
    (t=  <L> expression() {jjtThis.setLine(t.beginLine);} #l(2))
  )
}


void atomic_expression() #void : {} 
{
  variable() 
  |
  integer()
  |
  <LP> expression() <RP>
}


void multiplicative_expression() #void : {
	Token t;
}
{ 
  atomic_expression() 
  (
    ( t=<TIMES> atomic_expression() {jjtThis.setLine(t.beginLine);} #mul(2) )
    |
    ( t=<DIV> atomic_expression() {jjtThis.setLine(t.beginLine);} #div(2) )
  )*
}


void additive_expression() #void : {
	Token t;
}
{ 
  multiplicative_expression() 
  (
    ( t=<PLUS> multiplicative_expression() {jjtThis.setLine(t.beginLine);} #plus(2) )
    |
    ( t=<MINUS> multiplicative_expression() {jjtThis.setLine(t.beginLine);} #minus(2) )
  )*
}

void expression() #void : {}
{
  additive_expression()
}


